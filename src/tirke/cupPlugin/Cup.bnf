{
    generate=[names="long"]

    parserClass = "tirke.cupPlugin.parser.CupParser"
    parserUtilClass="tirke.cupPlugin.parser.CupParserUtil"
    extends = "com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix = "Cup"
    psiImplClassSuffix = "Impl"
    psiPackage = "tirke.cupPlugin.psi"
    psiImplPackage = "tirke.cupPlugin.psi.impl"


    elementTypeHolderClass = "tirke.cupPlugin.psi.CupTypes"
    elementTypeClass = "tirke.cupPlugin.psi.CupElementType"
    tokenTypeClass = "tirke.cupPlugin.psi.CupTokenType"



    tokens = [

    //Punctuation
    BAR = "|"
    CCEQ = "::="
    COLON = ":"
    COMMA = ","
    LEFTCUPBRACES = "{:"
    RIGHTCUPBRACES = ":}"
    DOT = "."
    SEMI = ";"
    STAR = "*"

    // Java keywords
    CLASS = "class"
    IMPORT = "import"
    PACKAGE = "package"
    STATIC = "static"

    // Cup keywords
    ACTION = "action"
    CODE = "code"
    INIT = "init"
    PARSER = "parser"
    SCAN = "scan"
    START = "start"
    WITH = "with"

    // Cup symbols keywords
    NON = "non"
    NONTERMINAL = "nonterminal"
    TERMINAL = "terminal"

    // Cup precedence keywords
    LEFT = "left"
    NONASSOC = "nonassoc"
    PERCENT_PREC = "%prec"
    PRECED = "precedence"
    RIGHT = "right"

    // Regex
    identifier = "regexp:\w+"
    comment="regexp:/\*(.|\n)*\*/"
    space='regexp:\s+'



    ]

    pin(".*Code")=1
    pin(".*Declaration")=1
    pin(".*Definition")=1

}


cupSpec                 ::=  packageDeclaration? importDeclaration* classNameDeclaration? codeParts* symbols+
                         precedences* startWith? production+

packageDeclaration      ::= 'package' qualifiedName ';'
importDeclaration       ::= 'import' 'static'? qualifiedName ('.' '*')? ';'
classNameDeclaration    ::= 'class' identifier ';'

private codeParts       ::= actionCode | parserCode | initCode | scanCode
actionCode              ::= 'action' 'code' java_code ';'?
parserCode              ::= 'parser' 'code' java_code ';'?
initCode                ::= 'init' 'with' java_code ';'?
scanCode                ::= 'scan' 'with' java_code ';'?


private symbols             ::= terminalDefinition | nonTerminalDefinition
                            |   typedNonTerminalDefinition | typedTerminalDefinition

//private symbols_recover     ::= !('terminal' | 'non' | 'nonterminal')
terminalDefinition          ::= 'terminal' termNameList
typedTerminalDefinition     ::= 'terminal' typeId termNameList
nonTerminalDefinition       ::= ( 'non' 'terminal' |'nonterminal' )  termNameList
typedNonTerminalDefinition  ::= ( 'non' 'terminal' |'nonterminal' ) typeId termNameList


precedences             ::= 'precedence' ('left' | 'right' | 'nonassoc') termNameList
startWith               ::= 'start' 'with' identifier ';'


production              ::= rule_id '::=' rhs_list ';' {pin = 2}
rhs_list                ::= rhs ('|' rhs)*
rhs                     ::= prod_part_list '%prec' term_id
                        |   prod_part_list
prod_part_list          ::= prod_part*
prod_part               ::= symbol_id opt_label?
                        |   java_code


java_code               ::= '{:' JAVA* ':}' {implements= "com.intellij.psi.PsiLanguageInjectionHost"
                                             mixin = "tirke.cupPlugin.psi.impl.CupJavaImpl"}
opt_label               ::= COLON label_id
typeId                  ::= qualifiedName
rule_id                 ::= identifier
symbol_id               ::= identifier
term_id                 ::= identifier
label_id                ::= identifier

termNameList            ::= identifier (COMMA identifier)* ';'
qualifiedName           ::= identifier (DOT identifier)*

