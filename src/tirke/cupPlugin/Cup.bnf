{
    generate=[names="long"]

    parserClass = "tirke.cupPlugin.parser.CupParser"
    parserUtilClass="tirke.cupPlugin.parser.CupParserUtil"
    extends = "com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix = "Cup"
    psiImplClassSuffix = "Impl"
    psiPackage = "tirke.cupPlugin.psi"
    psiImplPackage = "tirke.cupPlugin.psi.impl"
    psiImplUtilClass = "tirke.cupPlugin.psi.impl.CupPsiImplUtil"


    elementTypeHolderClass = "tirke.cupPlugin.psi.CupTypes"
    elementTypeClass = "tirke.cupPlugin.psi.CupElementType"
    tokenTypeClass = "tirke.cupPlugin.psi.CupTokenType"



    tokens = [

    //Punctuation
    BAR = "|"
    CCEQ = "::="
    COLON = ":"
    COMMA = ","
    LEFTCUPBRACES = "{:"
    RIGHTCUPBRACES = ":}"
    DOT = "."
    SEMI = ";"
    STAR = "*"

    // Java keywords
    CLASS = "class"
    IMPORT = "import"
    PACKAGE = "package"
    STATIC = "static"

    // Cup keywords
    ACTION = "action"
    CODE = "code"
    INIT = "init"
    PARSER = "parser"
    SCAN = "scan"
    START = "start"
    WITH = "with"

    // Cup symbols keywords
    NON = "non"
    NONTERMINAL = "nonterminal"
    TERMINAL = "terminal"

    // Cup precedence keywords
    LEFT = "left"
    NONASSOC = "nonassoc"
    PERCENT_PREC = "%prec"
    PRECED = "precedence"
    RIGHT = "right"

    // Regex
    identifier = "regexp:\w+"
    line_comment='regexp://.*'
    block_comment='regexp:/\*((?<!\*/)(.|\n))*'
    space='regexp:\s+'



    ]

    pin(".*Code")=1
    pin(".*Declaration")=1

}


cupSpec                 ::=  packageDeclaration? importDeclaration* classNameDeclaration? codeParts* symbols*
                         precedences* startWith? production*

packageDeclaration      ::= 'package' qualifiedName ';'
importDeclaration       ::= 'import' 'static'? qualifiedName ('.' '*')? ';'
classNameDeclaration    ::= 'class' identifier ';'

private codeParts       ::= actionCode | parserCode | initCode | scanCode
actionCode              ::= 'action' 'code' java_code ';'?
parserCode              ::= 'parser' 'code' java_code ';'?
initCode                ::= 'init' 'with' java_code ';'?
scanCode                ::= 'scan' 'with' java_code ';'?


symbols                 ::= ( 'terminal' | 'non' 'terminal' | 'nonterminal' )
                            ( typeId symbolDefinition (',' symbolDefinition)*
                            | symbolDefinition (',' symbolDefinition)* )  ';' {pin = 1}

//private symbols_recover   ::= !('terminal' | 'non' | 'nonterminal')

symbolDefinition        ::= identifier {implements = "tirke.cupPlugin.psi.CupNamedElement"
                                       extends = "tirke.cupPlugin.psi.impl.CupNamedElementImpl"}




precedences             ::= 'precedence' ('left' | 'right' | 'nonassoc') termNameList
startWith               ::= 'start' 'with' identifier ';'


production              ::= rule_id '::=' rhs ('|' rhs)* ';' {pin = 2}
rhs                     ::= prod_part* ['%prec' term_id]
prod_part               ::= symbol_id (':' label_id)?
                        |   java_code


java_code               ::= '{:' JAVA* ':}' {implements= "com.intellij.psi.PsiLanguageInjectionHost"
                                             mixin = "tirke.cupPlugin.psi.impl.CupJavaImpl"}
typeId                  ::= qualifiedName
rule_id                 ::= identifier
symbol_id               ::= identifier { methods = [getReference] }
term_id                 ::= identifier
label_id                ::= identifier

termNameList            ::= identifier (',' identifier)* ';'
qualifiedName           ::= identifier ('.' identifier)*

